from nat import Nat
numerals Nat

inductive List<T> {
    nil
    cons(T, List<T>)
}

class List<T> {
    // Adding two lists concatenates them
    define add(self, other: List<T>) -> List<T> {
        match self {
            List.nil {
                other
            }
            List.cons(head, tail) {
                List.cons(head, tail.add(other))
            }
        }
    }

    // Whether this list contains a particular item.
    define contains(self, item: T) -> Bool {
        match self {
            List.nil {
                false
            }
            List.cons(head, tail) {
                if head = item {
                    true
                } else {
                    tail.contains(item)
                }
            }
        }
    }
}

theorem add_nil<T>(list: List<T>) {
    list + List.nil<T> = list
}

theorem nil_add<T>(list: List<T>) {
    List.nil<T> + list = list
}

theorem add_contains_left<T>(left: List<T>, right: List<T>, item: T) {
    left.contains(item) implies (left + right).contains(item)
} by {
    define f(x: List<T>) -> Bool {
        x.contains(item) implies (x + right).contains(item)
    }
    f(List.nil)
    forall(head: T, tail: List<T>) {
        if f(tail) {
            f(List.cons(head, tail))
        }
    }
}

theorem add_contains_right<T>(left: List<T>, right: List<T>, item: T) {
    right.contains(item) implies (left + right).contains(item)
} by {
    define f(x: List<T>) -> Bool {
        (x + right).contains(item)
    }
    f(List.nil)
    forall(head: T, tail: List<T>) {
        if f(tail) {
            f(List.cons(head, tail))
        }
    }
}

class List<T> {
    define contains_every(self) -> Bool {
        forall(x: T) {
            self.contains(x)
        }
    }

    define length(self) -> Nat {
        match self {
            List.nil {
                Nat.0
            }
            List.cons(_, tail) {
                tail.length.suc
            }
        }
    }
}

theorem add_length<T>(left: List<T>, right: List<T>) {
    left.length + right.length = (left + right).length
} by {
    define f(x: List<T>) -> Bool {
        x.length + right.length = (x + right).length
    }
    f(List.nil)
    forall(head: T, tail: List<T>) {
        if f(tail) {
            f(List.cons(head, tail))
        }
    }
}

class List<T> {
    let singleton: T -> List<T> = function(x: T) {
        List.cons(x, List.nil<T>)
    }

    // Remove all the duplicates in the list.
    // The last one is kept, to make the implementation simpler.
    define unique(self) -> List<T> {
        match self {
            List.nil {
                List.nil<T>
            }
            List.cons(head, tail) {
                if tail.contains(head) {
                    tail.unique
                } else {
                    List.cons(head, tail.unique)
                }
            }
        }
    }
}

theorem unique_length<T>(list: List<T>) {
    list.unique.length <= list.length
} by {
    define f(x: List<T>) -> Bool {
        x.unique.length <= x.length
    }
    f(List.nil)
    forall(head: T, tail: List<T>) {
        if f(tail) {
            tail.unique.length <= tail.length
            if tail.contains(head) {
                List.cons(head, tail).unique.length <= List.cons(head, tail).length
            } else {
                List.cons(head, tail).unique.length <= List.cons(head, tail).length
            }
            f(List.cons(head, tail))
        }
    }
}

class List<T> {
    define append(self, item: T) -> List<T> {
        self + List.singleton(item)
    }
}

// A list of the numbers from 0 to n-1.
define range(n: Nat) -> List<Nat> {
    match n {
        Nat.0 {
            List.nil<Nat>
        }
        Nat.suc(n) {
            range(n).append(n)
        }
    }
}

class List<T> {
    let range: Nat -> List<Nat> = range

    define filter(self, f: T -> Bool) -> List<T> {
        match self {
            List.nil {
                List.nil<T>
            }
            List.cons(head, tail) {
                if f(head) {
                    List.cons(head, tail.filter(f))
                } else {
                    tail.filter(f)
                }
            }
        }
    }
}

// Let's see if any LLM can prove this theorem.
theorem length_range(n: Nat) {
    List.range(n).length = n
} by {
    // Induction on n
    define f(x: Nat) -> Bool {
        List.range(x).length = x
    }

    // Base case: List.range(0).length = 0
    List.range(0) = List.nil<Nat>
    List.nil<Nat>.length = Nat.0
    f(0)

    // Inductive step
    forall(x: Nat) {
        if f(x) {
            // Induction hypothesis: List.range(x).length = x
            List.range(x.suc) = range(x.suc)
            List.range(x.suc) = range(x).append(x)
            List.range(x.suc) = range(x) + List.singleton(x)

            // Use add_length theorem
            range(x).length + List.singleton(x).length = (range(x) + List.singleton(x)).length
            range(x).length + List.singleton(x).length = List.range(x.suc).length

            // Simplify the left side
            List.range(x).length = x
            List.singleton(x).length = Nat.1
            x + Nat.1 = x.suc
            x + List.singleton(x).length = x.suc
            List.range(x).length + List.singleton(x).length = x.suc

            // Therefore
            List.range(x.suc).length = x.suc
            f(x.suc)
        }
    }

    // Conclusion
    f(n)
}

define map<T, U>(items: List<T>, f: T -> U) -> List<U> {
    match items {
        List.nil {
            List.nil<U>
        }
        List.cons(head, tail) {
            List.cons(f(head), map(tail, f))
        }
    }
}

from monoid import AddComm

define sum<A: AddComm>(items: List<A>) -> A {
    match items {
        List.nil {
            A.zero
        }
        List.cons(head, tail) {
            head + sum(tail)
        }
    }
}

define sum_range<A: AddComm>(f: Nat -> A, n: Nat) -> A {
    sum(map(List.range(n), f))
}

theorem map_add<T, U>(left: List<T>, right: List<T>, f: T -> U) {
    map(left + right, f) = map(left, f) + map(right, f)
} by {
    define p(x: List<T>) -> Bool {
        map(x + right, f) = map(x, f) + map(right, f)
    }

    // Base case: map(nil + right, f) = map(nil, f) + map(right, f)
    map(List.nil<T> + right, f) = map(right, f)
    map(List.nil<T>, f) = List.nil<U>
    List.nil<U> + map(right, f) = map(right, f)
    p(List.nil)

    // Inductive step
    forall(head: T, tail: List<T>) {
        if p(tail) {
            // Induction hypothesis: map(tail + right, f) = map(tail, f) + map(right, f)

            // Left side: map(List.cons(head, tail) + right, f)
            map(List.cons(head, tail) + right, f) = map(List.cons(head, tail + right), f)
            map(List.cons(head, tail + right), f) = List.cons(f(head), map(tail + right, f))

            // Use induction hypothesis
            List.cons(f(head), map(tail + right, f)) = List.cons(f(head), map(tail, f) + map(right, f))

            // Right side: map(List.cons(head, tail), f) + map(right, f)
            map(List.cons(head, tail), f) = List.cons(f(head), map(tail, f))
            map(List.cons(head, tail), f) + map(right, f) = List.cons(f(head), map(tail, f)) + map(right, f)
            List.cons(f(head), map(tail, f)) + map(right, f) = List.cons(f(head), map(tail, f) + map(right, f))

            // Therefore
            map(List.cons(head, tail) + right, f) = map(List.cons(head, tail), f) + map(right, f)
            p(List.cons(head, tail))
        }
    }
}

theorem add_assoc<T>(a: List<T>, b: List<T>, c: List<T>) {
    (a + b) + c = a + (b + c)
} by {
    define p(x: List<T>) -> Bool {
        (x + b) + c = x + (b + c)
    }

    // Base case: (nil + b) + c = nil + (b + c)
    (List.nil<T> + b) + c = b + c
    List.nil<T> + (b + c) = b + c
    p(List.nil)

    // Inductive step
    forall(head: T, tail: List<T>) {
        if p(tail) {
            // Induction hypothesis: (tail + b) + c = tail + (b + c)

            // Left side: (List.cons(head, tail) + b) + c
            (List.cons(head, tail) + b) + c = List.cons(head, tail + b) + c
            List.cons(head, tail + b) + c = List.cons(head, (tail + b) + c)

            // Use induction hypothesis
            List.cons(head, (tail + b) + c) = List.cons(head, tail + (b + c))

            // Right side: List.cons(head, tail) + (b + c)
            List.cons(head, tail) + (b + c) = List.cons(head, tail + (b + c))

            // Therefore
            (List.cons(head, tail) + b) + c = List.cons(head, tail) + (b + c)
            p(List.cons(head, tail))
        }
    }
}
