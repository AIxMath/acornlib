from add_monoid import AddMonoid

// An additive group is an additive monoid that also has inverses.
typeclass A: AddGroup extends AddMonoid {
    neg: A -> A

    inverse_right(a: A) {
        a + -a = A.zero
    }
}

// This direction is proven rather than assumed
theorem inverse_left<A: AddGroup>(a: A) {
    -a + a = A.zero
} by {
    (-a + a) + -a = -a
    (-a + a) + -a + --a = -a + --a
    (-a + a) + (-a + --a) = -a + --a
    (-a + a) + A.zero = A.zero
}

theorem inverse_inverse<A: AddGroup>(a: A) {
    --a = a
} by {
    a + -a = A.zero
    a + -a + --a = --a
}

theorem left_cancel<A: AddGroup>(a: A, b: A, c: A) {
    a + b = a + c implies b = c
} by {
    -a + (a + b) = -a + (a + c)
}

theorem right_cancel<A: AddGroup>(a: A, b: A, c: A) {
    b + a = c + a implies b = c
} by {
    (b + a) + -a = (c + a) + -a
}

theorem inverse_add<A: AddGroup>(a: A, b: A) {
    -(a + b) = -b + -a
} by {
    -(a + b) + (a + b) = A.zero
    -(a + b) + a = -b
}

